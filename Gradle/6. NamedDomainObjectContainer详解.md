---

title: NamedDomainObjectContainer详解

date: 2021-01-23 

categories: 

   - Gradle

tags: 

   - Gradle 

description: ​

---

from:
作者：云飞扬1
链接：https://www.jianshu.com/p/167cd4b82653
来源：简书
著作权归作者所有。商业转载请联系作者获得授权，非商业转载请注明出处。

<!-- TOC -->

- [NamedDomainObjectContainer的使用场景](#nameddomainobjectcontainer的使用场景)
- [NamedDomainObjectContainer是什么](#nameddomainobjectcontainer是什么)
- [怎么创建NamedDomainObjectContainer](#怎么创建nameddomainobjectcontainer)
- [查找和遍历](#查找和遍历)

<!-- /TOC -->

# NamedDomainObjectContainer的使用场景

前面在讲解 Gradle Extension 的时候，说到名为 android 的 Extension 是由 BaseExtension 这个类来实现的，里面对 buildTypes 是这样定义的：

```groovy
private final NamedDomainObjectContainer<BuildType> buildTypes;
```

buildTypes 就是 NamedDomainObjectContainer 类型的，先来看看 buildTypes 在 Android 中是怎么使用的，下面这段代码应该都很熟悉了，它定义了 debug、relase 两种打包模式：

```groovy
android {

    buildTypes {
        release {
            // 是否开启混淆
            minifyEnabled true
            // 开启ZipAlign优化
            zipAlignEnabled true
            //去掉不用资源
            shrinkResources true
            // 混淆文件位置
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // 使用release签名
            signingConfig signingConfigs.hmiou
        }

        debug {
            signingConfig signingConfigs.hmiou
        }
    }
}
```

当我们新建一个项目的时候，默认会有 debug、release 这2个配置，那么问题来了：debug、release 能不能修改为其他名字？能不能增加其他的名字来配置，比方说我想增加一个测试包配置 test ？还有就是 release 里面都能配置哪些属性呢？

我来说下结果，如果不确定的，可以实际验证一下：

1. debug、release 是可以修改成其他名字的，你可以替换成你喜欢的名字；
2. 你可以增加任意不同名字的配置，比如增加一个开发版本的打包配置 dev ；
3. 可配置的属性可参考接口：com.android.builder.model.BuildType ；

可以看到它是非常灵活的，你可以根据不同的场景定义不同的配置，每个不同的命名空间都会生成一个 BuildType 配置。要实现这样的功能，必须使用 NamedDomainObjectContainer 类型。


# NamedDomainObjectContainer是什么

顾名思义就是命名领域对象容器，它的主要功能有：

* 它能够通过DSL(在Gradle脚本中)创建指定 type 的对象实例；
* 指定的 type 必须有一个 public 构造函数，且必须带有一个 String name 的参数，type 类型的领域对象必须有名为“name”的属性；
* 它是一个实现了 SortedSet 接口的容器，所以所有领域对象的 name 属性值都必须是唯一的，在容器内部会用 name 属性来排序；


```java
A named domain object container is a specialisation of NamedDomainObjectSet that adds the ability to create instances of the element type.

Note that a container is an implementation of SortedSet, which means that the container is guaranteed to only contain elements with unique names within this container. Furthermore, items are ordered by their name.
```

我觉得总结起来就2个：
1. NamedDomainObjectContainer是个容器
2. 可以帮我们在gradle脚本中创建对象。

比如我们常写的 buildTypes：

```groovy
buildTypes {
        release {
            // 是否开启混淆
            minifyEnabled true
        }

        debug {
            signingConfig signingConfigs.hmiou
        }
}
```

buildTypes 就是个容器，它可以根据我们指定的 名字（release、debug这些），自动帮我们创建对象，然后存储到这个容器中。同时，还给这个对象指定了一些属性。比如 minifyenabled 这些



# 怎么创建NamedDomainObjectContainer

NamedDomainObjectContainer 需要通过 Project.container(...) API 来创建，其定义为：
```groovy
<T> NamedDomainObjectContainer<T> container​(Class<T> type)
<T> NamedDomainObjectContainer<T> container​(Class<T> type, NamedDomainObjectFactory<T> factory)
<T> NamedDomainObjectContainer<T> container​(java.lang.Class<T> type, Closure factoryClosure
```

来看个具体的实例：

```groovy
//这是领域对象类型定义
//这个说的是要 NamedDomainObjectContainer容器帮我们管理什么类型的对象
class TestDomainObj {
    
    //必须定义一个 name 属性，并且这个属性值初始化以后不要修改
    String name

    String msg

    //构造函数必须有一个 name 参数
    public TestDomainObj(String name) {
        this.name = name
    }

    void msg(String msg) {
        this.msg = msg
    }

    String toString() {
        return "name = ${name}, msg = ${msg}"
    }
}

//创建一个扩展，有了扩展，就可以在project里配置了
//我的疑问是，不跟Extension一起用，可以么？
class TestExtension {

    //定义一个 NamedDomainObjectContainer 属性
    NamedDomainObjectContainer<TestDomainObj> testDomains

    //构造
    public TestExtension(Project project) {
        //通过 project.container(...) 方法创建 NamedDomainObjectContainer 
        NamedDomainObjectContainer<TestDomainObj> domainObjs = project.container(TestDomainObj)
        testDomains = domainObjs
    }

    //让其支持 Gradle DSL 语法
    //类似于 Extension 中的 inner。必须这样写一下，让gradle能调用
    void testDomain(Action<NamedDomainObjectContainer<TestDomainObj>> action) {
        action.execute(testDomains)
    }

    void test() {
        //遍历命名领域对象容器，打印出所有的领域对象值
        testDomains.all { data ->
            println data        
        }
    }
}

//创建一个名为 test 的 Extension
// 第一个参数是 Extension的名字，第二个参数是对应的类型，第三个数传入构造方法的参数
def testExt = getExtensions().create("test", TestExtension, project)

test {
    testDomain {
        domain2 {
            msg "This is domain2"
        }
        domain1 {
            msg "This is domain1"
        }
        domain3 {
            msg "This is domain3"
        }
    }   
}


task myTask  {
    testDomains.all { data ->
        println data        
    }
}

//我的疑问是，不跟Extension一起用，可以么？
//我们不创建Extension，直接用ext行不行

// 这样貌似不行，因为ext里面可能根本不认识testDomain
// 因为在ext里，没有声明这个方法
// void testDomain(Action<NamedDomainObjectContainer<TestDomainObj>> action) {
//   action.execute(testDomains)
// }
// ext {
//     testDomain {
//         domain2 {
//             msg "This is domain2"
//         }
//         domain1 {
//             msg "This is domain1"
//         }
//         domain3 {
//             msg "This is domain3"
//         }
//     }   
// }

// 这里就报错了



```


运行结果如下：
```shell
name = domain1, msg = This is domain1
name = domain2, msg = This is domain2
name = domain3, msg = This is domain3
```

#  查找和遍历

NamedDomainObjectContainer 既然是一个容器类，与之相应的必然会有查找容器里的元素和遍历容器的方法：

```groovy
//遍历
void all(Closure action)
//查找
<T> T getByName(String name)
//查找
<T> T findByName(String name)
```

还是接着前面的例子：

```groovy
//通过名字查找
TestDomainObj testData = testDomains.getByName("domain2")
println "getByName: ${testData}"

//遍历命名领域对象容器，打印出所有的领域对象值
testDomains.all { data ->
    println data        
}                   
```

需要注意的是，Gradle 中有很多容器类的迭代遍历方法有 each(Closure action)、all(Closure action)，但是一般我们都会用 all(...) 来进行容器的迭代。all(...) 迭代方法的特别之处是，不管是容器内已存在的元素，还是后续任何时刻加进去的元素，都会进行遍历。

