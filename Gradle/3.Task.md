---

title: Task

date: 2021-01-23 

categories: 

   - Task

tags: 

   - Task 

description: ​

---

<!-- TOC -->

- [创建Task](#创建task)
- [任务依赖](#任务依赖)
- [Task 配置阶段](#task-配置阶段)
- [Gradle 生命周期](#gradle-生命周期)
- [<< 操作](#-操作)
- [创建Task的几种常见写法](#创建task的几种常见写法)
- [创建Task的参数介绍](#创建task的参数介绍)
- [通过任务名来操作任务](#通过任务名来操作任务)
- [Task Interface](#task-interface)
- [TaskContainer接口解析](#taskcontainer接口解析)
- [任务排序](#任务排序)
- [任务启用和禁用](#任务启用和禁用)
- [任务的onlyIf](#任务的onlyif)

<!-- /TOC -->

# 创建Task

创建一个Task 非常简单


```groovy

task hello {

    doFirst {
        println "hello::doFirst"
    }

    doLast {
        println "hello::doFirst"
    }
}
```

这里的task看着像关键字，其实他是 project对象的一个函数。原型是：

```groovy
task(String taskName, Closure configureClosue)
```

那正常调用方法的时候，我们应该是这样写的：

```groovy
project.task("hello", {
    // 任务做的操作，比如doFirst，doLast
})
```

这个project对象，其实就是当前的 build.gradle，也就是当前的工程对象，因为Gradle在初始化的时候，Gradle为每个项目创建一个Project实例。

这里我们调用project对象的task()方法。第一个参数指定Task的名称，第二个参数是个闭包，用于对创建的任务进行配置。

根据Groovy的规定，当最后一个参数是闭包的时候，可以拿到括号外面，就成了下面这样。

```groovy
project.task("hello"){
    // 任务做的操作，比如doFirst，doLast
}
```

然后方法调用，括号可以不写。同时，调用project的方法或者属性，project也可以省略，就成了下面的模样。

```groovy
task "hello" {
    // 任务做的操作，比如doFirst，doLast
}
```
最终，字符串双引号也麻烦，省略吧。



在闭包里面执行的  doFirst 和 doLast， 都是Task的方法。原型：doFirst(Closure action)
因此，最最完整，可能是这样的吧：

```groovy
project.task("hello", {
    hello.doFirst({})
    hello.doLast({})
})
```


doFirst 方法是在任务执行前执行的。
doLast 方法是在任务执行后执行的。

那任务执行是什么？ Action。action：指执行阶段被运行的代码块。

```groovy
class PrintVersion extends DefaultTask{
    @TaskAction
    void start(){
        println("on action")
    }
}
```
然后通过type来指定。


# 任务依赖

任务之间是可以有依赖关系的。通过dependsOn 来指定。

```groovy
task intro(dependsOn: hello) {
    doLast {
        println "im Gradle"
    }
}
```

输出：

```shell
ShanhongdeiMac:helloworld shanhongfan$ gradlew intro
Starting a Gradle Daemon (subsequent builds will be faster)

> Configure project :

> Task :hello
Hello world!

> Task :intro
im Gradle

BUILD SUCCESSFUL in 3s
2 actionable tasks: 2 executed
```

先执行 hello 任务，再执行 intro。

另外，一个任务也可以同时依赖多个任务；

```groovy
task intro2 {
    dependsOn hello, intro
    doLast {
        println "intro2"
    }
}

```

这里 dependsOn 是Task 类的一个方法，可以接收多个任务作为参数。
原型：

```groovy
dependsOn(tasks: Object...)
```


# Task 配置阶段


```groovy
task myTask1 {
    println "configure task1"
}

task myTask2 {
    println "configure task2"
}
```

执行其中一个task：gradle myTask1

```shell
> Configure project :
configure task1
configure task2

```

上面定义了2个 task ：myTask1、myTask2，但是当我们执行 myTask1 时，发现2个 task 括号内部的代码都被执行了。括号内部的代码我们称之为配置代码，在 gradle 脚本的配置阶段都会执行，也就是说不管执行脚本里的哪个任务，所有 task 里的配置代码都会执行。


# Gradle 生命周期
Task 的运行与 Gradle 的生命周期息息相关，所以在此之前先介绍一下 Gradle 构建的生命周期，无论什么时候执行 Gradle 构建，都会依次运行三个不同的生命周期阶段：

* 初始化
在初始化阶段，Gradle 会解析 setting.gradle 文件获取该项目包含几个子项目，然后创建一个 RootProject 以及分别为子项目创建 Project 实例。

* 配置
初始化完成后进入配置阶段，此时会加载所有 build.gradle 文件配置及插件，然后执行所有 Task 的配置代码块。

* 执行
执行指的就是依据顺序执行所有 Task 的动作。

需要注意，无论我们是单独运行某一个 Task，还是运行所有的 Task，Gradle 的生命周期都是固定为上述的三个步骤，只不过执行的时候会有选择的执行指定 Task 及其依赖的 Task，这意味着如果一个 Task 设置了在配置阶段执行某项任务，即使我们运行了别的 Task，该任务也会被执行。

![](https://cdn.jsdelivr.net/gh/fanshanhong/note-image/task.jpg)


#  << 操作
已废弃
参考：

![](https://cdn.jsdelivr.net/gh/fanshanhong/note-image/gradle_%3C%3C_error.png)



![](https://cdn.jsdelivr.net/gh/fanshanhong/note-image/gradle_%3C%3C_solve.png)

# 创建Task的几种常见写法

```groovy
task myTask1 {
    doLast {
        println "doLast in task1"
    }
}

task myTask2 << {
    println "doLast in task2"
}

//采用 Project.task(String name) 方法来创建
project.task("myTask3").doLast {
    println "doLast in task3"
}

//采用 TaskContainer.create(String name) 方法来创建
project.tasks.create("myTask4").doLast {
    println "doLast in task4"
}

project.tasks.create("myTask5") << {
    println "doLast in task5"
}
```

# 创建Task的参数介绍
在 Gradle 中定义 Task 的时候，可以指定更多的参数，如下所示：
|  参数名   | 含义  | 默认值|
|  ----  | ----  | ----  | 
|name	|task的名字	|必须指定，不能为空| 
|type	|task的父类	|默认值为org.gradle.api.DefaultTask| 
|overwrite	|是否替换已经存在的同名task	|false| 
|group	|task所属的分组名	|null| 
|description|	task的描述	|null| 
|dependsOn	|task依赖的task集合	|无| 
|constructorArgs|	构造函数参数|	无| 

我们来测试下：
```groovy
task myTask1 << {
    println "doLast in task1"
}

task myTask2 << {
    println "doLast in task2"
}

task myTask3 << {
    println "doLast in task3, this is old task"
}

task myTask3(description: "这是task3的描述", group: "myTaskGroup", dependsOn: [myTask1, myTask2], overwrite: true) << {
    println "doLast in task3, this is new task"
}
```
执行 gradle myTask3，结果如下：
```shell
> Task :myTask1
doLast in task1

> Task :myTask2
doLast in task2

> Task :myTask3
doLast in task3, this is new task
```

执行命令 gradle -q tasks --all，查看下 task 信息，节选我们创建的 task 信息如下：

```shell
MyTaskGroup tasks
------------
myTask3 - 这是task3的描述

Other tasks
-----------
myTask1
myTask2
```

上面例子中创建了2个名为 myTask3 的 task，但是后一个将前一个替换掉了，在分组信息里多了个一个名为 MyTaskGroup 的分组，其他没有命名分组的统一归到 Other 这个分组里去了。

我们再来看看 type 参数怎么个用法，在 Gradle 中通过 task 关键字创建的 task，默认的父类都是 org.gradle.api.DefaultTask，这里定义了一些 task 的默认行为。看看下面这个例子：

```groovy
//自定义Task类，必须继承自DefaultTask
class SayHelloTask extends DefaultTask {
    
    String msg = "default name"
    int age = 18        

    //构造函数必须用@javax.inject.Inject注解标识
    @javax.inject.Inject
    SayHelloTask(int age) {
        this.age = age
    }

    //通过@TaskAction注解来标识该Task要执行的动作
    @TaskAction
    void sayHello() {
        println "Hello $msg ! age is ${age}"
    }

}

//通过constructorArgs参数来指定构造函数的参数值
task hello1(type: SayHelloTask, constructorArgs: [30])

//通过type参数指定task的父类，可以在配置代码里修改父类的属性
task hello2(type: SayHelloTask, constructorArgs: [18]) {
        //配置代码里修改 SayHelloTask 里的字段 msg 的值
    msg = "hjy"
}
```

执行这2个 task 查看运行结果如下：

```shell
> Task :hello1
Hello default name ! age is 30

> Task :hello2
Hello hjy ! age is 18
```

看到了，这里就是指定Action的方式。


在Android Studio 中，给Task 指定 group为：group BasePlugin.BUILD_GROUP，才能在Gradle面板中看到，否则看不到的。
![](https://cdn.jsdelivr.net/gh/fanshanhong/note-image/gradle_group.png)




还有，在闭包中指定属性

```groovy
task hello {
    group BasePlugin.BUILD_GROUP
    description "hhhh"
    println("hello")
    def t = project.android.getBuildTypes()
    android.sourceSets.each {
        println it
    }
}
```

这里的 `group BasePlugin.BUILD_GROUP` 和 `description "hhhh"` 都是方法调用。
这样是简写的方法调用，gradle 会去找 description(String) 方法，或者是  setDescription(String) 方法，能找到就调用，找不到就报错。
如果同时存在description(String) 方法和setDescription(String)，是调用description(String)

也可以通过任务名称来指定。
```groovy
hello.description = "xxx"
```

# 通过任务名来操作任务

通过任务名，使用Task的API，可以访问Task的相关方法。

要使用任务名来操作任务，必须先声明，因为脚本是顺序执行的。


```groovy
// 这种写法明显是错误的。要依赖taskY，必须先声明
//task taskX(dependsOn: 'taskY')  {
//    println 'taskX'
//}
//task taskY  {
//    println 'taskY'
//}

task taskY {
    println 'taskY'
}

task taskX(dependsOn: taskY) {
    println 'taskX'
}
```


指定Task的doFirst 和 doLast
```groovy
hello.doLast {
    println "1111"
}

hello.doFirst {
    println "2222"
}

```

指定task的分组和描述

```groovy
hello.group = BsePlugin.BUILD_GROUP
hello.description = "这是描述"
//设置依赖的task，只有test1 task执行完后才会执行hello task
hello.dependsOn(taskX)
```


# Task Interface


Gradle 所说的 Task 是 org.gradle.api.Task 接口，默认实现是 org.gradle.api.DefaultTask 类，其类图大概如下（只节选了比较重要的部分）：

![](https://cdn.jsdelivr.net/gh/fanshanhong/note-image/task_interface.webp)


为了了解每个接口方法的含义，我们直接用代码来测试验证下：

```groovy
class SayHelloTask extends DefaultTask {
    
    String msg = "default name";
    int age = 20

    @TaskAction
    void sayHello() {
        println "Hello $msg ! Age is ${age}"
    }

}

task test1 << {
    println "task test1 exec..."
}
task test2 << {
    println "task test2 exec..."
}
task test3 << {
    println "task test3 exec..."
}
task hello(type: SayHelloTask, group: "MyGroup")

//对task进行配置，
hello.configure {
    println "hello task configure"
    msg = "hjy"
}

//获取task的名称
println "task name is ${hello.getName()}"
//获取task的组名
println "task group is ${hello.getGroup()}"

//设置task里的属性值，设置 age = 70
hello.setProperty("age", 70)
//获取task里的某个属性值
println "task msg is ${hello.property('msg')}"

//设置依赖的task，只有test1 task执行完后才会执行hello task
hello.dependsOn(test1)
//设置终结者任务，执行完hello task之后会执行test2 task，通常可以用该方法做一些清理操作
hello.finalizedBy(test2)

//如果同时执行hello、test3这2个task，会确保test3执行完之后才执行hello这个task，用这个来保证执行顺序
hello.setMustRunAfter([test3])

//设置满足某个条件后才执行该task
hello.setOnlyIf {
    //只有当 age = 70 时，才会执行task，否则不会执行
    return hello.property("age") == 70
}
```

我们执行任务gradle hello test3，结果如下：

```shell
> Configure project :
hello task configure
task name is hello
task group is MyGroup
task msg is hjy

> Task :test3
task test3 exec...

> Task :test1
task test1 exec...

> Task :hello
Hello hjy ! Age is 70

> Task :test2
task test2 exec...

```

# TaskContainer接口解析

TaskContianer 是用来管理所有的 Task 实例集合的，可以通过 Project.getTasks() 来获取 TaskContainer 实例。

```groovy
org.gradle.api.tasks.TaskContainer接口：
//查找task
findByPath(path: String): Task
getByPath(path: String): Task
getByName(name: String): Task
withType(type: Class): TaskCollection
matching(condition: Closure): TaskCollection

//创建task
create(name: String): Task
create(name: String, configure: Closure): Task 
create(name: String, type: Class): Task
create(options: Map<String, ?>): Task
create(options: Map<String, ?>, configure: Closure): Task

//当task被加入到TaskContainer时的监听
whenTaskAdded(action: Closure)
```

我们先来看看创建 task 的方法：

```groovy
//当有task创建时
getTasks().whenTaskAdded { Task task ->
    println "The task ${task.getName()} is added to the TaskContainer"
}

//采用create(name: String)创建
getTasks().create("task1")

//采用create(options: Map<String, ?>)创建
getTasks().create([name: "task2", group: "MyGroup", description: "这是task2描述", dependsOn: ["task1"]])

//采用create(options: Map<String, ?>, configure: Closure)创建
getTasks().create("task3", {
    group "MyGroup"
    setDependsOn(["task1", "task2"])
    setDescription "这是task3描述"
})
```

执行命令gradle -q tasks --all，查看是否创建成功，结果如下：

```shell
MyGroup tasks
-------------
task2 - 这是task2描述
task3 - 这是task3描述

Other tasks
-----------
task1
```

我们再来试试查找 task 的方法：

```groovy
//通过名字查找指定的task
def task3 = getTasks().findByName("task3")
println "findByName() return task is " + task3

def taskList = getTasks().withType(DefaultTask)
def count = 0
//遍历所有的task，打印出其名字
taskList.all { Task t ->
    println "${count++} task name is ${t.name}"
}
```


# 任务排序


```groovy
// taskB 应该在taskA之后执行，但是不能保证
taskB.showRunAfter(taskA)
// taskB必须在taskA之后执行，强制
taskB.mustRunAfter(taskA)
```

# 任务启用和禁用

task 中有个 enabled 属性，用于启用和禁用任务。默认是true，表示启用。如果设置为false，则禁止任务执行，该任务被跳过.

```groovy
task enableTask {
    doFirst {
        println "11122"
    }
}

enableTask.enabled = false
```

# 任务的onlyIf

断言就是一个条件表达式。Task 有个onlyIf 方法，接收一个闭包作为参数。如果该闭包返回true，则任务执行；否则跳过。



```groovy
//设置task里的属性值，设置 age = 70
hello.setProperty("age", 70)

//设置满足某个条件后才执行该task
hello.setOnlyIf {
    //只有当 age = 70 时，才会执行task，否则不会执行
    return hello.property("age") == 70
}
```